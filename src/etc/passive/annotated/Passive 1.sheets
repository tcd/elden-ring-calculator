/*

| cell |            name            |
| ---- | -------------------------- |
| E6   | Arc Scaling                |
| F8   | Passive Arcane CalcCorrect |
| J2   | Passive Type 1             |
| K2   | Passive Type 2             |
| K4   | Rot/Mad/Sleep 2            |
| L20  | Arc Requirement Met?       |
|      |                            |
| I8   | Frost Base 2               |
| J8   | Poison Base 2              |
| K8   | Blood Base 2               |

| cell |            name            |
| ---- | -------------------------- |
| F2   | Weapon                     |
| G2   | Upgrade Level              |
| F8   | Passive Arcane CalcCorrect |
| K6   | Blood Base 1               |
| J6   | Poison Base 1              |
| J4   | Rot/Mad/Sleep 1            |
| K4   | Rot/Mad/Sleep 2            |

*/

=IF(
    {Weapon} = "Cold Antspur Rapier",
    SWITCH(
        {Upgrade Level},
        0, 50,
        1, 55,
        2, 60,
        3, 65,
        4, 70,
        5, 75,
        0
    ),
    IF(
        OR(
            {Weapon} = "Poison Fingerprint Stone Shield",
            {Weapon} = "Blood Fingerprint Stone Shield"
        ),
        IF(
            {Arc Scaling} > 0,
            ADD(MULTIPLY({Arc Scaling},MULTIPLY({Passive Arcane CalcCorrect},{Rot/Mad/Sleep 1})),{Rot/Mad/Sleep 1}),
            {Rot/Mad/Sleep 1}
        ),
        IF(
            $L$20 = 0,
            SWITCH(
                $J$2,
                "Scarlet Rot", {Rot/Mad/Sleep 1} * 0.6,
                "Madness",     {Rot/Mad/Sleep 1} * 0.6,
                "Sleep",       {Rot/Mad/Sleep 1} * 0.6,
                "Frost",       {Frost Base 1}    * 0.6,
                "Poison",      {Poison Base 1}   * 0.6,
                "Blood",       {Blood Base 1}    * 0.6,
                0
            ),
            SWITCH(
                $J$2,
                "Scarlet Rot", {Rot/Mad/Sleep 1},
                "Madness",     {Rot/Mad/Sleep 1},
                "Sleep",       {Rot/Mad/Sleep 1},
                "Frost",       {Poison Base 1},
                "Poison", IF(
                    {Arc Scaling} > 0,
                    ADD(MULTIPLY({Arc Scaling},MULTIPLY({Passive Arcane CalcCorrect},{Poison Base 1})),{Poison Base 1}),
                    {Poison Base 1}
                ),
                "Blood", IF(
                    {Arc Scaling} > 0,
                    ADD(MULTIPLY({Arc Scaling},MULTIPLY({Passive Arcane CalcCorrect},{Blood Base 1})),{Blood Base 1}),
                    {Blood Base 1}
                ),
                0
            )
        )
    )
)
